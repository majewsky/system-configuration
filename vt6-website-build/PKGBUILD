pkgname=vt6-website-build
pkgver=4
pkgrel=1
pkgdesc='Generator for vt6.io'
arch=('x86_64')
url="https://github.com/vt6/website"
license=('GPL3')
depends=(
    'texlive-core'       # pdflatex, tikz.sty
    'texlive-latexextra' # standalone.cls
    'pdf2svg'
)
makedepends=('go')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('b00f141d93a4b68d2c4fc7d81334ccfca0baf008ce308db8330257efbabeb8c0')

options=('!strip') # binaries are already stripped inside the Makefile

build() {
    cd "${srcdir}/website-${pkgver}"
    make
}

package() {
    cd "${srcdir}/website-${pkgver}"
    make install DESTDIR="${pkgdir}"
}
